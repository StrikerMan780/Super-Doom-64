// D64Stuff Monster code credit goes to Footman.

actor 64ZombieMan : ZombieMan Replaces ZombieMan
{
  Yscale 0.70
  XScale 0.75
  PainChance "Vertigo", 256
  states
  {
  Spawn:
    POSS AB 12 A_Look
    loop
  See:
    POSS AABBCCDD 5 A_Chase
    loop
  Missile:
    POSS E 12 A_FaceTarget
    POSS F 10 BRIGHT A_PosAttack
    POSS E 10
    Goto See
  Pain:
    POSS G 3
    POSS G 3 A_PlaySound ("grunt/pain", CHAN_AUTO)
    Goto See
  Pain.Vertigo:
    POSS G 0 ThrustThingZ (0, 50, 0, 0)
    POSS G 3
    POSS G 3 A_PlaySound ("grunt/pain", CHAN_AUTO)
    Goto See
  Death:
    POSS H 6
    POSS I 6 A_Scream
    POSS J 6 A_NoBlocking
    POSS K 6
    POSS L -1
    stop
  XDeath:
    POSS M 6
    POSS N 6 A_XScream
    POSS O 6 A_NoBlocking
    POSS PQRST 6
    POSS U -1
    stop
  Raise:
    POSS LKJIH 6
    Goto See
  Crush:
    POL5 A 0
    POL5 A 0 A_PlaySound ("Misc/Gibbed")
    POL5 A -1
    Stop
  }
}

actor 64StealthZombieMan : 64ZombieMan Replaces StealthZombieMan
{
  +STEALTH
}

actor 64ShotgunGuy : ShotgunGuy Replaces ShotgunGuy
{
  Yscale 0.70
  XScale 0.75
  PainChance "Vertigo", 256
  states
  {
  Spawn:
    SPOS AB 12 A_Look
    loop
  See:
    SPOS AABBCCDD 4 A_Chase
    loop
  Missile:
    SPOS E 12 A_FaceTarget
    SPOS F 12 BRIGHT A_SposAttackUseAtkSound
    SPOS E 12
    Goto See
  Pain:
    SPOS G 3
    SPOS G 3 A_PlaySound ("shotguy/pain", CHAN_AUTO)
    Goto See
  Pain.Vertigo:
    SPOS G 0 ThrustThingZ (0, 50, 0, 0)
    SPOS G 3
    SPOS G 3 A_Pain
    Goto See
  Death:
    SPOS H 6
    SPOS I 6 A_Scream
    SPOS J 6 A_NoBlocking
    SPOS K 6
    SPOS L -1
    stop
  XDeath:
    SPOS M 6
    SPOS N 6 A_XScream
    SPOS O 6 A_NoBlocking
    SPOS PQRST 6
    SPOS U -1
    stop
  Raise:
    SPOS L 6
    SPOS KJIH 6
    goto See
  Crush:
    POL5 A 0
    POL5 A 0 A_PlaySound ("Misc/Gibbed")
    POL5 A -1
    Stop
  }
}

actor 64StealthShotgunGuy : 64ShotgunGuy Replaces StealthShotgunGuy
{
  +STEALTH
}

actor 64Demon : Demon Replaces Demon
{
	YScale 0.80
	XScale 0.85
	PainChance "Vertigo", 256
	states
	{
	Spawn:
	SARG BD 12 A_Look
	loop
	See:
	SARG AABBCCDD 2 A_Chase
	loop
	Melee:
	SARG EF 10 A_FaceTarget
	SARG G 10 A_SargAttack
	Goto See
	Pain:
	SARG H 3
	SARG H 3 A_PlaySound ("demon/pain", CHAN_AUTO)
	Goto See
	Pain.Vertigo:
	SARG H 0 ThrustThingZ (0, 50, 0, 0)
	SARG H 3
	SARG H 3 A_PlaySound ("demon/pain", CHAN_AUTO)
	Goto See
	Death:
	SARG I 9
	SARG J 9 A_Scream
	SARG K 5
	SARG L 5 A_NoBlocking
	SARG M 5
	SARG N -1
	stop
  	Crush:
    	POL5 A 0
    	POL5 A 0 A_PlaySound ("Misc/Gibbed")
    	POL5 A -1
    	Stop
	}
}

actor 64StealthDemon : 64Demon Replaces StealthDemon
{
	+STEALTH
}

actor 64Spectre : Spectre Replaces Spectre
{
  YScale 0.80
  XScale 0.85
  Renderstyle Translucent
  PainChance "Vertigo", 256
  Alpha 0.25
  -SHADOW
  states
  {
  Spawn:
    SAR2 BD 10 A_Look
    Goto Spawn
  See:
    SAR2 AABBCCDD 2 A_Chase
    goto See
  Melee:
    SAR2 EF 10 A_FaceTarget
    SAR2 G 10 A_SargAttack
    goto See
  Pain:
    SAR2 H 3
    SAR2 H 3 A_PlaySound ("spectre/pain", CHAN_AUTO)
    goto See
  Pain.Vertigo:
    SAR2 H 0 ThrustThingZ (0, 50, 0, 0)
    SAR2 H 3
    SAR2 H 3 A_PlaySound ("spectre/pain", CHAN_AUTO)
    goto See
  Death:
    SAR2 I 9
    SAR2 J 9 A_Scream
    SAR2 K 5
    SAR2 L 5 A_NoBlocking
    SAR2 M 5
    SAR2 N -1
    stop
  Raise:
    SAR2 NMLKJI 5
    goto See
  Crush:
    POL5 A 0
    POL5 A 0 A_PlaySound ("Misc/Gibbed")
    POL5 A -1
    Stop
  }
}

actor 64DoomImp : DoomImp Replaces DoomImp
{
  Yscale 0.70
  XScale 0.75
  meleesound "imp/melee"
  meleedamage 3
  missiletype DoomImpBall
  missileheight 40
  PainChance "Vertigo", 256
  states
  {
  Spawn:
    TROO AB 12 A_Look
    loop
  See:
    TROO AABBCCDD 4 A_Chase
    loop
  Melee:
    TROO EF 8 A_FaceTarget
    TROO G 8 A_MeleeAttack
    goto See
  Missile:
    TROO IJ 8 A_FaceTarget
    TROO K 8 A_MissileAttack
    goto See
  Pain:
    TROO H 3
    TROO H 3 A_PlaySound ("imp/pain", CHAN_AUTO)
    goto See
  Pain.Vertigo:
    TROO H 0 ThrustThingZ (0, 50, 0, 0)
    TROO H 3
    TROO H 3 A_PlaySound ("imp/pain", CHAN_AUTO)
    Goto See
  Death:
    TROO L 8
    TROO M 8 A_Scream
    TROO N 8
    TROO O 8 A_NoBlocking
    TROO P -1
    stop
  XDeath:
    TROO Q 6
    TROO R 6 A_XScream
    TROO S 6
    TROO T 6 A_NoBlocking
    TROO UVW 6
    TROO X -1
    stop
  Raise:
    TROO PO 8
    TROO NML 8
    goto See
  Crush:
    POL5 A 0
    POL5 A 0 A_PlaySound ("Misc/Gibbed")
    POL5 A -1
    Stop
  }
}

actor 64StealthDoomImp : 64DoomImp Replaces StealthDoomImp
{
	+STEALTH
}

actor 64Cacodemon : Cacodemon Replaces Cacodemon
{
  Yscale 0.70
  XScale 0.75
  +MISSILEMORE
  PainChance "Vertigo", 256
  states
  {
  Spawn:
    HEAD ABCD 15 A_Look
    loop
  See:
    HEAD AABBCCDD 4 A_Chase
    loop
  Pain:
    HEAD B 3
    HEAD B 6 A_PlaySound ("caco/pain", CHAN_AUTO)
    HEAD B 8
    goto See
  Pain.Vertigo:
    HEAD B 0 ThrustThingZ (0, 50, 0, 0)
    HEAD B 3
    HEAD B 6 A_PlaySound ("caco/pain", CHAN_AUTO)
    HEAD B 8
    Goto See
  Melee:
  Missile:
    HEAD EF 6 A_FaceTarget
    HEAD G 6 A_HeadAttack
    goto See
  Death:
    HEAD H 9
    HEAD I 9 A_Scream
    HEAD JK 9
    HEAD L 9 A_NoBlocking
    HEAD M -1 A_SetFloorClip
    stop
  Raise:
    HEAD M 9 A_UnSetFloorClip
    HEAD LKJIH 9
    goto See
  Crush:
    POL5 A 0
    POL5 A 0 A_PlaySound ("Misc/Gibbed")
    POL5 A -1
    Stop
  }
}

actor 64StealthCacodemon : 64Cacodemon Replaces StealthCacodemon
{
	+STEALTH
}

actor 64HellKnight Replaces HellKnight
{
  spawnid 113
  Yscale 0.70
  XScale 0.75
  obituary "%o was splayed by a Hell Knight"
  hitobituary "%o was gutted by a Hell Knight"
  health 500
  -BOSSDEATH
  bloodcolor "18 8c 31"
  radius 24
  height 64
  mass 1000
  speed 8
  painchance 50
  seesound "knight/sight"
  painsound "knight/pain"
  deathsound "knight/death"
  activesound "knight/active"
  MONSTER
  +FLOORCLIP
  PainChance "Vertigo", 256
  DamageFactor "Railgun", 2.0
  states
  {
  Spawn:
    BOSS A 0 A_SetShootable
    BOSS A 0 A_ChangeFlag ("NODAMAGE", 0)
    BOSS AB 12 A_Look
    Goto Spawn+2
  See:
    BOSS AABBCCDD 4 A_Chase
    loop
  Melee:
  Missile:
    BOSS EF 8 A_FaceTarget
    BOSS G 8 A_BruisAttack
    goto See
  Pain:
    BOSS H 3
    BOSS H 3 A_PlaySound ("knight/pain", CHAN_AUTO)
    goto See
  Pain.Vertigo:
    BOSS H 0 ThrustThingZ (0, 50, 0, 0)
    BOSS H 3
    BOSS H 3 A_PlaySound ("knight/pain", CHAN_AUTO)
    Goto See
  Death:
    BOSS I 9
    BOSS J 9 A_Scream
    BOSS K 9
    BOSS L 9 A_NoBlocking
    BOSS M 9
    BOSS N -1 A_BossDeath
    stop
  Raise:
    BOSS NMLKJI 9
    goto See
  Crush:
    POL5 A 0
    POL5 A 0 A_PlaySound ("Misc/Gibbed")
    POL5 A -1
    Stop
  }
}

actor 64StealthHellKnight : 64HellKnight replaces StealthHellKnight
{
	+STEALTH
}

actor 64BaronOfHell : BaronOfHell Replaces BaronOfHell
{
  bloodcolor "8D 39 10"
  Yscale 0.80
  XScale 0.85
  PainChance "Vertigo", 256
  DamageFactor "Railgun", 2.0
  states
  {
  Spawn:
    BOS2 A 0 A_SetShootable
    BOS2 A 0 A_ChangeFlag ("NODAMAGE", 0)
    BOS2 AB 12 A_Look
    Goto Spawn+2
  See:
    BOS2 AABBCCDD 4 A_Chase
    loop
  Melee:
  Missile:
    BOS2 EF 8 A_FaceTarget
    BOS2 G 8 A_BasicAttack (10, "baron/melee", "64BaronBall", 40)
    goto See
  Pain:
    BOS2 H 3
    BOS2 H 3 A_PlaySound ("baron/pain", CHAN_AUTO)
    Goto See
  Pain.Vertigo:
    BOS2 H 0 ThrustThingZ (0, 50, 0, 0)
    BOS2 H 3
    BOS2 H 3 A_PlaySound ("baron/pain", CHAN_AUTO)
    Goto See
  Death:
  	BOS2 I 9
	BOS2 J 9 A_Scream
	BOS2 K 9
	BOS2 L 9 A_NoBlocking
	BOS2 M 9
	BOS2 N -1 A_BossDeath
	stop
  Raise:
  	BOS2 NMLKJI 9
	goto See
  Crush:
    POL5 A 0
    POL5 A 0 A_PlaySound ("Misc/Gibbed")
    POL5 A -1
    Stop
  }
}